#!/usr/bin/perl -w

use strict;
use warnings;

my $upstream = get_repo('upstream');
my $origin = (split /\//, get_repo('origin'))[0];
my $branch = get_branch();

if ($branch eq 'master') {
    exit 0;
}

my $upstream_branch = 'master';

if ($branch =~ /-(\d)[.-]?(x|\d+)$/) {
    $upstream_branch = "$1.$2";
}

print <<EOF;

 ðŸŽ‰  Create a PR by opening https://github.com/$upstream/compare/$upstream_branch...$origin:$branch?expand=1

EOF


sub get_repo {
    my ($name) = @_;

    my $upstream = qx( git remote get-url $name 2>&1 );

    exit 0 if $upstream =~ /^fatal:/;

    chomp $upstream;

    my $repo;

    if ($upstream =~ m#^http#) {
        $repo = $upstream;
        $repo =~ s#^https?://github.com/##;
        return $repo;
    }

    ($repo) = (split /:/, $upstream)[1];
    $repo = (split /\./, $repo)[0];

    return $repo;
}

sub get_branch {
    my $name_rev = qx( git rev-parse --abbrev-ref HEAD );
    chomp $name_rev;
    return $name_rev;
}
